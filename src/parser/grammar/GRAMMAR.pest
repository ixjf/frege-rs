// TODO: Support for groupers, somehow clean this up, merging formula with statement

// This specification does not take into account these:
// 1. Degree of predicate matching number of terms
// 2. Binding variable that's already in use to quantifier
// 3. Predicate binding to variable that isn't in scope
// 4. (not yet implemented at all) Operator precedence
// For all else, if it doesn't match wiki/Language, it's
// a bug

WHITESPACE = _{ " " | NEWLINE | "\t" }

input = { SOI ~ (statement_set | argument) ~ EOI }

grouper_opening = _{ "(" }
grouper_closing = _{ ")" }

statement_separator = _{ "," }

statement_set_opening = _{ "{" }
statement_set_closing = _{ "}" }
statement_set = { 
    statement_set_opening ~ 
    statement ~ (statement_separator ~ statement)* ~ 
    statement_set_closing 
}

conclusion_indicator = _{ "\u{2234}" }
premise = { statement }
conclusion = { statement }
argument = {
    premise ~ (statement_separator ~ premise)* ~
    conclusion_indicator ~
    conclusion
}

formula = { 
	compound_formula | 
    atomic_formula
}

atomic_formula = {
	simple_predicate |
    simple_statement
}

compound_formula = {
    complex_statement |
	compound_formula_conjunction |
    compound_formula_negation |
    compound_formula_disjunction |
    compound_formula_conditional
}

compound_formula_conjunction = {
    grouper_opening ~ 
    formula ~ 
    conjunction_connective ~ 
    formula ~ 
    grouper_closing
}

compound_formula_negation = {
    grouper_opening ~ 
    negation_connective ~ 
    formula ~ 
    grouper_closing
}

compound_formula_disjunction = {
    grouper_opening ~ 
    formula ~ 
    disjunction_connective ~ 
    formula ~ 
    grouper_closing
}

compound_formula_conditional = {
    grouper_opening ~ 
    formula ~ 
    conditional_connective ~ 
    formula ~ 
    grouper_closing
}

statement = { complex_statement | simple_statement }
complex_statement = { 
    logical_conjunction | 
    logical_negation | 
    logical_disjunction | 
    logical_conditional |
    existential_statement |
    universal_statement
}

conjunction_connective = _{ "&" }
logical_conjunction = { 
    grouper_opening ~ 
    statement ~ 
    conjunction_connective ~ 
    statement ~ 
    grouper_closing 
}

negation_connective = _{ "~" }
logical_negation = { negation_connective ~ statement }

disjunction_connective = _{ "\u{2228}" }
logical_disjunction = { 
    grouper_opening ~ 
    statement ~ 
    disjunction_connective ~ 
    statement ~ 
    grouper_closing 
}

conditional_connective = _{ "\u{2283}" }
logical_conditional = { 
    grouper_opening ~ 
    statement ~ 
    conditional_connective ~ 
    statement ~ 
    grouper_closing 
}

subscript_number = ${ !"\u{2080}" ~ ('\u{2081}'..'\u{2089}'+ ~ "\u{2080}"*)+ }
simple_statement_letter_alpha = { ASCII_ALPHA_UPPER }
simple_statement_letter = ${ simple_statement_letter_alpha ~ subscript_number? }

simple_statement = {
    singular_statement |
    simple_statement_letter
}

singular_term_alpha = { !variable_alpha ~ ASCII_ALPHA_LOWER }
singular_term = ${ 
    singular_term_alpha ~ 
    subscript_number? 
}

variable_alpha = { 'x'..'z' }
variable = ${ variable_alpha ~ subscript_number? }

superscript_number = @{ 
    !"\u{2070}" ~ (
        ("\u{00B9}" | '\u{00B2}'..'\u{00B3}' | '\u{2074}'..'\u{2079}')+ ~ 
        "\u{2070}"*
    )+ 
}
predicate_letter_alpha = { ASCII_ALPHA_UPPER }
predicate_letter = ${
    predicate_letter_alpha ~ 
    subscript_number? ~
    superscript_number
}

singular_statement = ${ predicate_letter ~ singular_term+ }

simple_predicate = ${ 
    predicate_letter ~ 
    (singular_term* ~ variable ~ singular_term*)+ 
}

existential_quantifier = _{ "\u{2203}" }
existential_statement = { 
    existential_quantifier ~ 
    variable ~ 
    formula
}

universal_quantifier = _{ "\u{2200}" }
universal_statement = { 
    universal_quantifier ~ 
    variable ~ 
    formula
}
